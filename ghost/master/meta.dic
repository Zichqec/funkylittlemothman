//This file handles a couple little QOL functions you may want. But these are not necessary! If you don't care for them just erase them. If you don't want to set up network updates, you can comment or remove the update functions too. If you're not interested in any of these, just remove the whole file!

//—————————————————————————————— Installing something ——————————————————————————————
OnInstallComplete
{
	"\0%(typing):> Dude, I just found a new %(reference0)<: :> It's called %(reference1), you should totally check it out<:"
	--
	if "ghost" _in_ reference0 //A little menu to open or change to the ghost that was installed immediately
	{
		"\n\n\_q"
		--
		"\![*]\__q[OnInstallComplete.Action,call]Open %(reference0)\__q  \![*]\__q[OnInstallComplete.Action,change]Say goodbye and open %(reference0)\__q\n\n"
		--
		"\![*]\q[Thanks,blank]"
	}
}

OnInstallComplete.Action
{
	if reference0 == "change"
	{
		"\![change,ghost,lastinstalled]"
	}
	else //call
	{
		"\![call,ghost,lastinstalled]"
	}
}


//—————————————————————————————— Creating a .nar file ——————————————————————————————
OnNarCreated
{
	_nar = "\_a[OnNarLocation,""%(reference1)""]%(reference0)\_a" //Makes a clickable link that'll open the location of the file they just made
	
	"\0%(typing):> Grabbed a new %(_nar) for you<: :> You're welcome<:"
}

OnNarLocation
{	
	_path = SPLITPATH(reference0)[0] + SPLITPATH(reference0)[1]
	
	"\![open,file,""%(_path)""]"
}


//—————————————————————————————— Network Update ——————————————————————————————
//Starting an update
OnUpdateBegin
{
	"\0%(typing):> I need to update my stuff<: :> I wonder if anybody released anything new<:\w8"
}

//reference0 is the number of new files, starting from 0
OnUpdateReady
{
	reference0++ //Number of files starts from 0, so this displays the correct amount
	
	//plural checks, adds an s and changes is to are if there's more than 1 file
	_s = ""; _are = "is"; if reference0 != 1; _s = "s"; _are = "are"
	
	"\0%(typing):> omg there %(_are) %(reference0) new file%(_s)<:\w8"
}

//When the update finishes. Don't forget to initialize any new variables you've created! I highly highly recommend using OnInitialize for this (it's in boot.dic), in case the user updates via the ghost explorer or some other means.
//reference0 is 'none' if there were no new files to update with, reference1 is a list of new file names, separated by commas
OnUpdateComplete
{
	if reference0 == "none"
	{
		"\0%(typing):> No new files<: :> Tch<: :> I guess I can't complain, not like I updated any of my ghosts recently<:\w1\s[9]"
	}
	else
	{
		"\0%(typing)\i[6]:> Update is dooooooooone<: :> I wonder what they added<: :> I should've read the update log<:"
	}
}

//reference0 is the reason the update failed
OnUpdateFailure //If the update fails
{
	if reference0 == "timeout" //If it timed out
	{
		"\0%(typing)\i[6]:> Connection timed out<:\w1\s[9]"
	}
	elseif reference0 == "md5 miss" //If the update files have not been uploaded correctly and don't match the updates2.dau
	{
		_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //This gets the name and extension of the offending file
		
		"\0%(typing):> Oof, MD5 error on %(_file)<: :> that sucks<: :> mood tho<:"
	}
	elseif reference0 == "artificial" //If the user cancels the update by double clicking the balloon
	{
		"\0%(typing)\i[6]:> update cancelled wth<:\w1\s[9]"
	}
	else //Any other errors
	{
		"\0%(typing)]:> oof, got an error<: :> %(reference0)<:."
	}
}