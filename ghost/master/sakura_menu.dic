SakuraMenuGreetings : nonoverlap
{
	"Wassup."
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	"\0"
	"\![set,autoscroll,disable]"
	if !DefaultBalloon; "\b[0]"
	else; "\b[4]"
	
	//if SHIORI3FW.Eventid == "OnMouseDoubleClick"; SakuraMenuGreetings + "\n\n" //The check here is so they won't comment if you click the back button. Remove if you prefer
	
	"\_q"
	
	"\![*]\__q[OnAiTalk]Look at chat\__q"
	if LastTalk != ""; "  \![*]\__q[OnLastTalk]See chat log\__q"
	"\n\n"
	"\![*]\__q[OnTalkMenu]Talk\__q\n"
	"\![*]\__q[OnEncourageMenu]Encourage\__q\n\n"
	
	"\![*]\__q[OnSettings]Preferences\__q\n\n"
	
	"\![*]\__q[blank]Nevermind\__q"
	
	if SHIORI3FW.DebugMode; "\n\nProject: %(CurrentProject) (%((ProjectModeTime - GETSECCOUNT) / 60) minutes remaining)\nWork: %(CurrentWork) (%((WorkModeTime - GETSECCOUNT) / 60) minutes remaining)\nMood: %(CurrentMood) (%((MoodModeTime - GETSECCOUNT) / 60) minutes remaining)\nSurface: %(SurfaceModeTime - GETSECCOUNT)\n# of dialogues: %(OUTPUTNUM('RandomTalk'))"
}

OnLastTalk : all
{
	"\C\![lock,balloonrepaint]\c\0\b[2]\_q\*"
	
	foreach LastTalk; _talk
	{
		_talk + "\n"
	}
	"\n"
	"\![*]\__q[OnSakuraMenu]Back\__q  \![*]\__q[blank]Done\__q"
	"\![unlock,balloonrepaint]"
}

OnTalkMenu : all
{
	"\C\![lock,balloonrepaint]\c\0\_q"
	"\![set,autoscroll,disable]"
	
	if !DefaultBalloon; "\b[0]"
	else; "\b[4]"
	
	"\f[italic,1]Talk about...\f[italic,default]\n\n[99]"
	
	"\![*]\__q[OnTalk.Ghosts]Ghosts\__q\n"
	"\![*]\__q[OnTalk.HisGhosts]Your ghosts\__q\n"
	"\![*]\__q[OnTalk.CurrentProject]Your current project\__q\n"
	"\![*]\__q[OnTalk.GhostTips]Ghost tips\__q\n"
	"\![*]\__q[OnTalk.GhostIdeas]Ghost ideas\__q\n"
	"\n"
	"\![*]\__q[OnSakuraMenu]Back\__q  \![*]\__q[blank]Nevermind\__q"
	"\![unlock,balloonrepaint]"
	
}

OnTalk.Ghosts : nonoverlap_pool
{
	"\0%(typing):> My favourite part of working on a ghost<: :> Coding<: :> Because when I'm coding it usually means I'm already done with everything else lol<:"
	"\0%(typing):> I haven't really coded anything before trying out ghosts<: :> And now I feel like a wizard<:\i[6] :> And flex on my friends lol<:"
	"\0%(typing):> Ghosts are really cool<: :> They're like this niche medium for<: :> making whatever<: \i[6] :> Whatever your heart desires<: \s[1] :> It's a lot of fun<:"
	"\0%(typing):> I've learned a lot about ghosts over the last couple years working on them<: :> There's a lot of history<: :> and SO MANY japanese ghosts<: :> I hope there are that many english ghosts one day<: :> Guess I better keep at it<:"
	"\0%(typing):> Once you enter ghost hell you never leave<: \w8\w8\s[7]\w8\w8\w8\w8\s[1] :> that's a lie<: :> a lot of people have moved on to other things<: :> that's life I guess<: \w8\w8\w8 :> I'm here for life tho lol<:"
	"\0%(typing):> Ghosts can be so many things<: :> I was just making desktop buds when I first started<: :> but like<: :> idk man Egg was fun to make<: \i[6] :> so like<: :> make what you wanna make and have fun with it<:"
	"\0%(typing):> I wish I could get my other friends into ghosts<: :> they don't get the appeal tho<: :> I guess they're busy with other things lol<:"
	"\0%(typing):> I didn't expect to spend so much time on them<: :> I just thought it would be a fun side project to do once<: :> but now<: :> they are my life<:"
	"\0%(typing):> The more I work on them the more I learn<: :> the more I regret some of my past decisions lol<: :> but I'm getting a lot better at it<: :> And I'm learning how to program<: :> It's pretty neat<:"
	"\0%(typing):> Writing is so important<: :> that was something I struggled with for a bit<: :> like<: :> what do you even make them say when they're just standing there<: \w8\w8 :> But I've been working on that a lot lately<: :> Especially since I released Egg<: :> And I'm getting more confident in coming up with interesting things to say<: :> If the angle you picked is difficult, you can just pick a new one, you know?<:"
	"\0%(typing):> I'm glad I found ghosts<: :> I mean sometimes it's frustrating<: :> When stuff's broken<: :> or people are complaining<: :> or not reading your instructions<: :> or asking same question fifteen times<: :> or asking a hundred different questions<: :> or making unreasonable feature requests<: :> But making stuff is fun<:"
	--
	backbuttons
}

OnTalk.HisGhosts : all
{
	if !ProjectsDiscussed[0]
	{
		ProjectsDiscussed[0] = 1
		
		"\0%(typing):> Sure, I can tell you about my ghosts<: :> I have a lot of them tho<: :> You'll have to tell me which one you want to hear about<: :> I have 3 released ghosts: Chesster, Molnia, and Egg<: :> And 4 other ghosts in the works too<: :> Fae Friends Forever, Alastria, The Tragic Tale of Super Snail, and Printer<: :> So yeah, tell me which ones you want to know about<:\x"
	}
	
	"\C\![lock,balloonrepaint]\c\0\_q\f[anchorvisitedfontcolor,default.anchor]"
	"\![set,autoscroll,disable]"
	
	if !DefaultBalloon; "\b[2]"
	else; "\b[6]"
	
	"\f[italic,1]Which ghost?\nClick the choice markers to see a log of the original explanation.\f[italic,default]\n\n"
	
	switch ProjectsDiscussed[1]
	{
		"\![*]\_a[OnTalkGhost.OC]Chesster\_a\n"
		"\_a[OnTalkGhost.Initial,OC]\![*]\_a\__q[OnTalkGhost.OC]Chesster\__q\n"
	}
	switch ProjectsDiscussed[2]
	{
		"\![*]\_a[OnTalkGhost.Fandom]Molnia\_a\n"
		"\_a[OnTalkGhost.Initial,Fandom]\![*]\_a\__q[OnTalkGhost.Fandom]Molnia\__q\n"
	}
	switch ProjectsDiscussed[3]
	{
		"\![*]\_a[OnTalkGhost.Jam]Egg\_a\n"
		"\_a[OnTalkGhost.Initial,Jam]\![*]\_a\__q[OnTalkGhost.Jam]Egg\__q\n"
	}
	"\n"
	switch ProjectsDiscussed[4]
	{
		"\![*]\_a[OnTalkGhost.BigFandom]Fae Friends Forever\_a\n"
		"\_a[OnTalkGhost.Initial,BigFandom]\![*]\_a\__q[OnTalkGhost.BigFandom]Fae Friends Forever\__q\n"
	}
	switch ProjectsDiscussed[5]
	{
		"\![*]\_a[OnTalkGhost.MiniFandom]Alastria\_a\n"
		"\_a[OnTalkGhost.Initial,MiniFandom]\![*]\_a\__q[OnTalkGhost.MiniFandom]Alastria\__q\n"
	}
	switch ProjectsDiscussed[6]
	{
		"\![*]\_a[OnTalkGhost.Obscure]The Tragic Tale of Super Snail\_a\n"
		"\_a[OnTalkGhost.Initial,Obscure]\![*]\_a\__q[OnTalkGhost.Obscure]The Tragic Tale of Super Snail\__q\n"
	}
	switch ProjectsDiscussed[7]
	{
		"\![*]\_a[OnTalkGhost.Object]Printer\_a\n"
		"\_a[OnTalkGhost.Initial,Object]\![*]\_a\__q[OnTalkGhost.Object]Printer\__q\n"
	}

	"\n"
	"\![*]\__q[OnTalkMenu]Back\__q  \![*]\__q[blank]Nevermind\__q"
	"\![unlock,balloonrepaint]"
}

OnTalkGhost.Initial
{
	"\C\![lock,balloonrepaint]\c\0\b[2]\_q"
	--
	_text = EVAL("OnTalkGhost.Initial.%(reference0)")
	ChatlogFormatting(_text)
	--
	backbuttonsghost('nopause')
}

OnTalkGhost.Initial.OC
{
	"\0%(typing):> This one is my first ghost<: :> I kinda just made him up as I went<: :> His name is Chesster<: :> And he plays chess<: :> And that's it<: \w8\w8:> Weeeeell he's packed with default template functions<: :> 'cuz I followed a tutorial and template while I was making him<: :> But overall he just sits there and thinks about chess moves<: :> And that's pretty much it<:"
	
}

OnTalkGhost.Pool.OC : nonoverlap
{
	"\0%(typing):> Funny thing is<: :> I'm atrocious at playing chess<: :> But I made Chesster chess-themed because I made first surface for him black and white<: :> And rolled with it<:"
	"\0%(typing):> Chesster was his work-in-progress name<: :> I just put in something dumb when I decided on the whole chess theme<: :> But then idk I got attached?<: :> Silly little mothman<: :> me<:"
	"\0%(typing):> Imagine programming a working game of chess in YAYA<: :> Couldn't be me<: :> Because literally<: :> Skill issue<:"
	"\0%(typing):> So Chesster is usually all timid<: :> Just chess man doing chess stuff<: :> Thinking chess thoughts<: :> But he has<: :> A DARK SIIIDE<: :> tho rn I didn't really programm in him being angry<: :> I need to give user and option to flip the table or something<: :> But then Chesster will hate you forever<: :> Hmm...<:"
	"\0%(typing):> On some days I think<: :> Does anyone else even like Chesster?<: :> I mean he's a mess<: :> But<: :> idk<: :> I think he's kinda neat still<: :> Maybe it's just cuz I wroked on him for like two months and want someone to see it lol<:"
	"\0%(typing):> Look at this ghost man<: :> Just sitting at his table<: :> Thinking perpetually<: :> top ghost experience, am I right<:\w8\s[7]"
	"\0%(typing):> I don't make OCs usually<: :> but I do like Chesster<: :> I always talk down at this chess nerd and stuff<: :> but if someone else does it<: :> This mothman will get FUNKY<: :> In a threatening way<:\w8\s[7]"
	"\0%(typing):> I need to remake Chesster's surface<: :> Or add new ones<: :> I can never decide<: :> so I still haven't done either of these<:\w8\s[7]"
	"\0%(typing):> Chesster has a relationship system<: :> the more he's on our screen the more he likes you<: :> aside from a few random events<: :> where he gets tired and closes himself<: :> Terrible design<: :> but funny<:\w8\s[7]"
	"\0%(typing):> Chesster is a mess<: :> 'cuz when I made him I took out zero things from the template<: :> And added like<: :> every neat function I could get working<: :> For a man dude sitting at the table all the time he can do A LOT<:\w8\s[7]"
}

OnTalkGhost.OC : nonoverlap_pool
{
	if !ProjectsDiscussed[1]
	{
		ProjectsDiscussed[1] = 1
		OnTalkGhost.Initial.OC
	}
	else
	{
		OnTalkGhost.Pool.OC
	}
	--
	backbuttonsghost
}

OnTalkGhost.Initial.Fandom
{
	"\0%(typing)\i[6]:> Oh oh oh %(username) that's my most popular ghost<: :> If you heard about me before that's probably why<: :> But yeah it's Molnia from the TV show 'Fairyland Forever'<: :> She's one of the fairies<: :> The cool one with lightning powers<: :> Fandom looooves her so when I posted this ghost it got popular faaast<: :> Still feels wild<:"
	
}

OnTalkGhost.Pool.Fandom : nonoverlap
{
	"\0%(typing):> I tried to make Molnia as close to canon as possible<: :> Which was<: :> more difficult than I expected<: :> but people say I did a pretty good job so I'll believe them lol<:"
	"\0%(typing)\i[6]:> Dude dude dude you GOTTA show the Fairyland Forever<: :> Well not must<: :> But I really recommend it<: :> I know it looks like a kid's show about fairies<: :> but there's SO MUCH<: :> LORE<: :> ANIMATION<: :> CHARACTER DRAMA<: :> SECRETS<: :> THAT ONE SCENE OH GOD<: :> Watch it watch it trust me<: :> And if you watch it tell me<: :> Don't worry Molnia doesn't have a lot of spoilers<: :> Only like<: :> Up to season 8<:"
	"\0%(typing)\i[6]:> I'm really proud of the little animations I did on this one<: :> And also just the surfaces in general<: :> I spent soooooooo much time on the wings<: :> Worth it tho<:"
	"\0%(typing):> I have like 100 asks about Molnia in my inbox<: :> which is kinda anxiety inducing<: :> because I want to answer them<: :> but also make more updates to make those people happy<: :> but when I make updates I get even more asks<: :> idk man<:"
	"\0%(typing)\i[6]:> Molnia is the fun bubbly fairy<: :> She's not the comic relief tho<: :> Wendy is the comic relief<: :> but Molnia is just so cheery she makes you smile<:\s[1] :> And then they traumatized her<: :> And her past turned to be messed up too<: :> LORE<: :> LOOOORE<: :> I want to say so much but hhhh SPOILERS<:"
	"\0%(typing)\i[6]:> So I drew Molnia in her fairy outfit<: :> but I really want to add another shell for her usual outfit<: :> and season 4 transformation<: :> and one of the toys of her has such a cool look<:\s[1] :> I don't own any toys tho<: :> AND I HATE IT GAH<: :> DARN SHIPPING PRICE<:"
	"\0%(typing):> People's been making short videos about Molnia<: :> With thousand of views<: :> makes me wonder if I should record videos on my Molnia making experience<:\w8 :> but idk<:\w8 :> maybe<:"
	"\0%(typing):> I still need to update Molnia for the latest season of the show<: :> We learned a lot about her backstory<: :> But also its kinda<: :> Hard to write in??<: :> Like? How does it affect her on your screen??<: :> I don't know<: :> Maybe I'll throw a couple of dialogue lines referencing it<: :> But it feels so IMPORTANT<: :> Decisions, decisions<:"
	"\0%(typing):> It feels weird to program in sad stuff<: :> Like, the things that user can do that make Molnia sad<: :> people keep writing to me how they find attached they are to her<: :> so sometimes I feel like I make her too nice<: :> just so I don't traumatize people<: :> ghosts are supposed to be fun<: :> BUT<: :> CHARACTER CONSISTENCY<:"
	"\0%(typing)\i[6]:> Molnia is my second favourite character<: :> But I chose to make her into a ghost because I thought she'd work the best<: :> also she's easier to draw<: :> and easier to write<:"

}

OnTalkGhost.Fandom : nonoverlap_pool
{
	if !ProjectsDiscussed[2]
	{
		ProjectsDiscussed[2] = 1
		OnTalkGhost.Initial.Fandom
		
	}
	else
	{
		OnTalkGhost.Pool.Fandom
	}
	--
	backbuttonsghost
}

OnTalkGhost.Initial.Jam
{
	"\0%(typing):> I participated in a Ghost Jam once<: :> Since it was my first one I decided to go small<: :> So it's just<: :> an egg<: :> You click it and it moves sometimes<: :> And if you click it a lot you discover<: :> THE SECRET INSIIIIDE<: :> What's the secret?<: :> \i[6]Click the egg like a thousand times to find out<:"
	
}

OnTalkGhost.Pool.Jam : nonoverlap
{
	"\0%(typing):> Don't ask me why this egg took me a week<: :> It was my first jam<: :> Maybe I was nervous<: :> Or. lazy.<: :> Who can say<:"
	"\0%(typing):> I did make a lot of dress-ups for it<: :> I want to add more so badly<: :> but<: :> better add more functions first<: :> At least that's what I keep telling myself<:"
	"\0%(typing):> I went into the Jam with no real plan<: :> Just wanted to test my skills<: :> in the end<: :> I should've planned more lol<:"
	"\0%(typing):> Egg<: :> It's an egg<: :> what do you want me to say<: :> egg<: :> Egg<: :> THE Egg<:"
	"\0%(typing):> Initially egg was supposed to be simple<: :> Like all eggs are<: :> but then I wanted to give a player a joke choice<: :> but that choice opened like nine different paths for the player to take<: :> and I commited to it<: :> Just for the joke<:\w8\w4 \i[6]:> I mean<: :> Worth it<:"
	"\0%(typing):> I wonder if I should remake surface I made for egg during the jam<: :> You know, make EGG HD Edition<: :> or I should just keep the old ones<: :> They're not thaaat bad<:"
	"\0%(typing):> I remember when I described Egg idea to my friends<: :> One of them wanted it to hatch into something<: :> and another wanted to smash it<: :> and they started to yell in caps at each other<: :> and I was just sitting there<: :> with my egg<: :> even more confused with what to do<:"
	"\0%(typing):> Honestly<: :> Egg might be my most polished ghost<: :> That thought makes me low-key depressed<:"
	"\0%(typing):> I personally like my eggs fried<:"
	"\0%(typing):> I think for this ghost I've done most of the work on the last Jam day<: :> Because for most of the week I was like<: :> I have time<: :> I can take a break<: :> And on last day I sat there into the night<: :> working working working<: :> Right until the end of the Jam<:"
	"\0%(typing):> People were kinda :[ at me at the end of the egg<: :> But I think its poetic<: :> and sweet<: :> and I ran out of time and didn't have any other ideas<:"
}

OnTalkGhost.Jam : nonoverlap_pool
{
	if !ProjectsDiscussed[3]
	{
		ProjectsDiscussed[3] = 1
		OnTalkGhost.Initial.Jam
	}
	else
	{
		OnTalkGhost.Pool.Jam
	}
	--
	backbuttonsghost
}

OnTalkGhost.Initial.BigFandom
{
	"\0%(typing):> I have this really ambitious idea for another ghost based on 'Fairyland Forever'<: :> It's gonna be<: :> B I G<: :> It's gonna have like 3 characters, a big progression with lots of stuff to discover<: :> And I really want the shell to be expressive<: :> but that means<: :> SO much drawing<: \w8\w8 :> if you don't hear back from me assume that working on this ghost killed me<: \w8\w8\w8 :> Anyways I'm really excited about it<: :> But it's really scary too<: :> If you're familiar with the show, I'm planning to include Diane, Elliot, and Yoyo<: :> So yeah<: \i[6] :> Fun<:"
	
}

OnTalkGhost.Pool.BigFandom : nonoverlap
{
	"\0%(typing):> The ghost is going to be kind of a rag-tag found family sort of thing<: :> I love those kinds of stories<: :> and I really wish these characters in the show got more attention<: :> One of them, Diane, knows Molnia<: :> But that means I probably have to figure out how to get two different ghosts to interact...<:"
	"\0%(typing):> I'm going to have to write<: :> SO MUCH dialogue<: :> so much<: \w8\w8\s[6] :> It'll be pretty cool if I can pull it off tho<: \s[1]:> I guess I'll just have to keep chipping away at it<: :> But first I need to get the shell done...<: :> oh god it's so much to draw<:"
	"\0%(typing):> Since there are 3 characters I want to make it so that they face each other when talking<: :> Since, you know, the user might rearrange the order they're standing in<: :> but it's going to be<: :> hell<: :> because that instantly doubles the amount of poses I need for them talking to each other<: :> Don't be like me, keep your projects a reasonable size<:"
	"\0%(typing):> I'm not too worried about writing Diane, I feel like her dialogue will come to me easily<: :> but \f[italic,1]Elliot\f[italic,default]<: :> he's going to be hard to get right<: :> he's just so<: :> so<: :> \f[italic,1]intense\f[italic,default]<: :> He's easy to mess up and I want to do him justice<:"
	"\0%(typing):> Yoyo is a really underrated character I think<: :> I see a lot of people complaining she's annoying, so I guess I need to be careful when writing her<: :> but the group wouldn't be the same without her, you know?<: :> She's important too<:"
	"\0%(typing):> I know I'm setting my sights really high on this one<: :> I may never be able to complete it<: :> But I'm really excited about this idea<: :> If I keep working at it I have to get somewhere eventually right?<:"
	"\0%(typing):> Working on three characters at once is<: :> A lot of work<: :> Especially drawing-wise<: :> But it's also pretty fun<: :> Really different from one character ghost<: :> Exciting!!<:"
	"\0%(typing):> I was sooo tempted to add a forth character, too<: :> But I think I'll stick with Diane, Elliot and Yoyo for now<: :> for now<: :> That's more than enough work for me<:\w8\w5 :> for now<:"
	"\0%(typing):> I don't know if having three characters at once will make writing dialogue easier<: :> Or sooooo much worse<: :> So many combinations!!<: :> But also<: :> Sooo many combinations... to utilize them all fairly and properly...<:"
	"\0%(typing):> I hope people won't mind a couple of my headcanons<: :> Nobody seemed to mind them with Molnia, but...<: :> idk<: :> fandoms are vicious, unforgiving places sometimes<:"
}

OnTalkGhost.BigFandom : nonoverlap_pool
{
	if !ProjectsDiscussed[4]
	{
		ProjectsDiscussed[4] = 1
		OnTalkGhost.Initial.BigFandom
	}
	else
	{
		OnTalkGhost.Pool.BigFandom
	}
	--
	backbuttonsghost
}

OnTalkGhost.Initial.MiniFandom
{
	"\0%(typing)\i[6]:> Ok ok ok I'm so excited about this one<: :> Like, it's just a little thing, right?<: :> A side project while I keep chipping away at the Big One<: :> It's also related to 'Fairyland Forever'<: :> But this time it's one little side character, Alastria, and it's gonna be a really different kind of story from the other ghosts I'm working on<: \s[1] :> It's inspired by some Japanese ghosts I was messing with<: :> I wanted to try writing the same style of story<: :> I think fans of my other ghosts are going to find it kind of weird<: :> But I hope the reaction is 'wow this is cool' and not 'wow this sucks'<: :> You know?<: :> It's kinda scary, because it really could go either way<:"	
}

OnTalkGhost.Pool.MiniFandom : nonoverlap
{
	"\0%(typing):> The shell for this ghost is going to be fairly small I hope<: :>FAIRYly small you can say<: :> I'm trying to keep the scope in general really small and focused<: :> It's so easy to want to make it bigger and bigger<: :> But I don't have time<: :> I got soooo many other projects to work on<: :> I need to keep this one small and just make it good<: \w8\w8\w8\w8 :> I want to add a lot more tho<:"
	"\0%(typing):> I still need to sit down and plot how her story progression is going to go exactly<: :> It's the kind of thing where I need a plan in place you know<: :> There are only a few branches, but it's really important they all work together<: :> One of these days I'll sit down and figure out the details<:"
	"\0%(typing):> She's going to be in a different art style from the other ghosts I'm working on for 'Fairyland Forever'<: :> People might find that weird too<: :> idk<: :> but I don't want to always have to make them the same way you know<: :> So variety is good<:"
	"\0%(typing):> This ghost isn't going to talk to any of the other 'Fairyland Forever' ghosts<: :> It's going to be all self contained<: :> Because<: :> It takes place at a different point in the timeline<: :> And in a specific place<: :> So it would be really difficult and weird to have them talk<:"
	"\0%(typing):> Once I get my planning done and finish the shell I think the rest will go pretty fast<: :> There's a lot to write<: :> but like<: :> there's not anything else super complicated<:"
	"\0%(typing):> The idea for this ghost came to me while I was trying to sleep one night<: :> I had been looking at some cool Japanese ghosts<: :> they're really hard to understand even through machine translation if you would believe it<: :> Anyways, I was wondering if I could make anything with that style of story<: :> And then it hit me all at once and I knew Alastria was the one<: \w8\w8\w8 :> I wrote down so many plans on my phone that night<: :> and then I couldn't sleep lol<:"
	"\0%(typing):> I remember rewatching the show after I made Molnia<: :> Thinking of ghost possibilities<: :> And some people even wrote to me whether or not I'd ever make an Alastria ghost<: :> And back then I was like NO WAY<: :> I got ZERO ideas on Alastria and I don't see anything new I could've done with her I haven't made for Molnia already<: :> but look at me now<: :> @foolishlittlemothman<:"
	"\0%(typing):> I feel like I'll get a lot of ??? asks about this ghost when I finally make her<: :> If I ever finish making her<: :> Which I hopefully will<: :> Tho it might be just<: :> Anxiety lol<:"
	"\0%(typing):> Honestly I think I have most fun working on Alastria than any of my other ghosts<: :> She's just so DIFFERENT<:\w8 :> No offense, Chesster<:\w8\w8 :> No offense, Printer<:"
	"\0%(typing):> Coolest thing about Alastria<: :> She has<: :> MOTH WINGS<: :> IT WAS MEANT TO BE<:"
}

OnTalkGhost.MiniFandom : nonoverlap_pool
{
	if !ProjectsDiscussed[5]
	{
		ProjectsDiscussed[5] = 1
		OnTalkGhost.Initial.MiniFandom
		
	}
	else
	{
		OnTalkGhost.Pool.MiniFandom
	}
	--
	backbuttonsghost
}

OnTalkGhost.Initial.Obscure
{
	"\0%(typing):> If you haven't watched 'Starlight Sword Squad 3000'<: :> go. watch. it.<: :> please<: :> it's so underrated<: :> nobody ever knows what 3s3 is but it's really good<: :> The whole dynamic with Super Snail and Birdie Beauty is so complex<: :> I don't want to spoil it for you if you don't know<: :> But I'm making a ghost of Super Snail<: \w8\w8\w8 :> Even if nobody else knows who she is<: :> I'll know<: \i[6] :> And maybe other 3s3 fans will find it and join me in ghost hell<:"
	
}

OnTalkGhost.Pool.Obscure : nonoverlap
{
	"\0%(typing):> BirdSnail shippers might get upset with me for this ghost<: :> But I have a lot of feelings about BirdSnail<: :> I guess I'll put some warnings so people know what angle I'm taking<: :> Snailkey fans probably aren't going to be happy with me either lol<:"
	"\0%(typing):> It's really sad that the show only got a few seasons<: :> But hey<: :> Plenty of space for fans to make our own content right<: :> Like my ghost<: :> Maybe this ghost will get more people into the show<: \i[6] :> Then I would have more people to chat with about it<:"
	"\0%(typing):> I've been debating if Meteor Monkey should make an appearance as a side character<: :> Not like, all the time, but it might be nice as an event now and then<: :> That's a lot of extra surfaces to draw tho...<: \w8\w8\w8\w8 :> Maybe in an update<:"
	"\0%(typing):> This ghost is going to spoil the whole show<: :> So I guess I will put strong warnings on it<: \w8\w8 ::> go<:: ::> watch<:: ::> 3s3<::"
	"\0%(typing)\i[6]:> 3s3 is my favorite<: :> And Super Snail is my favorite 3s3 character<: :> So I have to<: :> You know?<: :> Probably nobody will see it<: :> But that's ok<: :> I want her to exist on my computer<: :> Would be nice if it got a few fans though lol<:"
	"\0%(typing):> I really need to get this ghost correct<: :> Because some of my friends and just ghost hell community people check out my ghosts even without knowing the source material<: :> So I need to make Super Snail so good it will make people want to watch the show!!<:"
	"\0%(typing):> I used to be afraid of snails as a kid<: :> Well, not afraid<: :> They were just so<: :> Gooey<: :> AND THEY TURNED INTO SLUGS<: :> But then I watched 3s3<:\i[6] :> And I can never hate snails now<: :> Thank you Super Snail<:"
	"\0%(typing):> A part of me hopes people would see this ghost<: :> Even people who have never seen Ghosts before<: :> Or the show before<: :> And go<: :> Look!! A cool looking thing!<: :> Show it came from must be cool, too!<: :> I wonder if it's worth a shot!<: :> AND IT IS IT'S WORTH SO MANY SHOTS<: :> But yeah that's my hopes and dreams for Super Snail<:"
	"\0%(typing):> Dude<: :> %(username)<: :> The pain of being into something<: :> And NONE OF YOUR FRIENDS RELATING<: :> Is so real<: :> Even the 3s3 fandom is t i n y<: :> Need more content need more content<: :> More snail content more 3s3 content more more more<:"
	"\0%(typing):> Since I love the characters and the show so much it makes me want to expand this ghost sooo much<: :> Like I have ideas for a lot of cool features<: :> Like joining the Starlight Sword Squad and having missions<: :> And ranks<: :> But that requires so much work<: :> And writing<: :> And thinking<: :> It's tough<: :> I need to plan better<: :> I can always update and add cool stuff in later<:"
}

OnTalkGhost.Obscure : nonoverlap_pool
{
	if !ProjectsDiscussed[6]
	{
		ProjectsDiscussed[6] = 1
		OnTalkGhost.Initial.Obscure
		
	}
	else
	{
		OnTalkGhost.Pool.Obscure
	}
	--
	backbuttonsghost
}

OnTalkGhost.Initial.Object
{
	"\0%(typing):> Ever since I made the Egg ghost I've become kind of obsessed with object ghosts<: :> like they don't seem like much at first right<: :> but actually it's really freeing<: :> because you get to ditch the standard format and make something silly<: :> A lot less drawing, too<: :> Anyways, so after Ghost Jam I decided I wanted to make another one<: :> This time it's gonna be a printer<: :> but the best bit is<: :> the printer always has some kind of printing trouble<: :> so you have to piece together what it's saying from bits and pieces<: :> rather than seeing the whole picture all at once<: :> I just think it will be fun<:"
}

OnTalkGhost.Pool.Objects : nonoverlap
{
	"\0%(typing):> What does a printer talk about?<: \i[6] :> Printer self-care, for starters<: :> Make sure you run your diagnostics<: :> Make sure to refill your ink regularly<: :> If you think your ink is getting low, cause a fuss until someone gets you more<: :> Don't settle for sub-par paper<: \s[1] :> That kind of stuff<:"
	"\0%(typing):> I'm debating if I want to add beeping noises<: :> I guess if I do I will need a volume control<: :> Or a way to turn it off<: \w8\w8 :> Sounds get pretty annoying<:"
	"\0%(typing):> I think I want to have extra characters that are sheets of paper<: :> And they have different images printed on them<: :> So you can collect and display them<: :> that is a lot of extra art tho...<:"
	"\0%(typing):> I don't want to make this ghost too complicated<: :> just like<: :> 1 or 2 solid mechanics<: :> and then a lot of dialogue<:"
	"\0%(typing):> I don't know how I'm going to sell people on this printer ghost<: :> Unless they're like<: :> Big printer fans lol<: :> I guess other fans of object ghosts will probably be the main audience<: :> we'll see<:"
	"\0%(typing):> I think it would be \f[strike,1]annoying\f[strike,default] fun if the printer complains about being out of ink every hour or so<: \i[6] :> gotta make it realistic you know<: :> give people The Home Printer Experience(tm)<: :> that everyone is looking for<:"
	"\0%(typing):> I hope Egg ghost won't get jealous<: :> Tho I can make it and Printer friends<: :> the Printegg duo!<: :> ... it sounded better in my head<:"
	"\0%(typing):> I'm considering adding some horror content<: :> Maybe with some way to toggle it on and off<: :> So that people who want a more chill experience don't have to worry about it lol<:"
	"\0%(typing):> I hope people don't think that like<: :> This will actually access their printer or something<: :> bet I get a few asks about that<:"
	"\0%(typing):> Printer is going to be so annoying lol<: :> but in like<: :> a fun way<: :> if you get this ghost you are signing up to be hassled by it<:"
}

OnTalkGhost.Object : nonoverlap_pool
{
	if !ProjectsDiscussed[7]
	{
		ProjectsDiscussed[7] = 1
		OnTalkGhost.Initial.Object
	}
	else
	{
		OnTalkGhost.Pool.Objects
	}
	--
	backbuttonsghost
}

OnTalk.CurrentProject : nonoverlap_pool
{
	if CurrentProject == "OC Ghost"
	{
		if CurrentWork == "Programming"
		{
			"\0%(typing):> Coding stuff for Chesster now<: :> I need to overhaul this stuff eventually<: :> but for now we carry on<:"
		}
		if CurrentWork == "Drawing"
		{
			"\0%(typing):> Drawing more poses for Chesster<: :> I need to draw chess pieces moving on board<: :> Wish me luck<:"
		}
		if CurrentWork == "Writing"
		{
			"\0%(typing):> Doing some writing for Chesster<: :> Time to give this man crisis<: :> He gives me crisis sometimes so I think that's only fair<:"
		}
	}
	if CurrentProject == "Fandom Ghost"
	{
		if CurrentWork == "Programming"
		{
			"\0%(typing):> Currently working on Molnia<: :> People pointed so many bugs<: :> Well, one bug but like ten people sent it to me<: :> Even after I posted that I'm working on it<: :> Well, I'm about to work on it rn<:"
			"\0%(typing):> Currently working on Molnia<: :> I promised fans a new big update<: :> Gonna start chipping on it bit by bit<:"
		}
		if CurrentWork == "Drawing"
		{
			"\0%(typing):> Drawing stuff for Molnia<: :> I need a couple of new poses<: :> Hopefully I can just copy the wings and not redraw them again<:"
		}
		if CurrentWork == "Writing"
		{
			"\0%(typing):> Writing more things for Molnia<: :> I usually either write like twenty lines for her in half a hour<: :> Or one line in one sitting<: :> The duality of writing<:"
		}
	}
	if CurrentProject == "Jam Ghost"
	{
		if CurrentWork == "Programming"
		{
			"\0%(typing):> Currently trying to fix Egg<: :> My code is so sloppy<: :> But I'll make it work<: :> Well, it's already working<: :> but I'll make it work BETTER<:"
		}
		if CurrentWork == "Drawing"
		{
			"\0%(typing):> Drawing another shell for the Egg<: :> Another EGG SHELL<: :> ...I always make this joke I love this joke it's so dumb<:"
		}
		if CurrentWork == "Writing"
		{
			"\0%(typing):> Writing dialogue for the Egg<: :> It's harder than you might think<:"
		}
	}
	if CurrentProject == "Big Fandom Ghost"
	{
		if CurrentWork == "Programming"
		{
			"\0%(typing):> Currently working on Fae Friends Forever<: :> Trying to fix a bug in their progression<: :> just have to track down where it is lol<:"
		}
		if CurrentWork == "Drawing"
		{
			"\0%(typing):> Drawing the shell for Fae Friends Forever<: :> It's taking a while<: :> they need so many poses<: :> for 3 characters<: :> facing both left and right<:"
		}
		if CurrentWork == "Writing"
		{
			"\0%(typing):> Writing dialogue for Fae Friends Forever<: :> It's pretty fun<: :> With 3 characters there's a lot of back and forth<: :> Makes it easier honestly<: :> you just say a thing and then the others carry it on for a while<:"
		}
	}
	if CurrentProject == "Mini Fandom Ghost"
	{
		if CurrentWork == "Programming"
		{
			"\0%(typing):> I'm programming for Alastria right now<: :> It's really important that I balance this system just right<: :> and probably work out this bug that keeps haunting me lol<:"
		}
		if CurrentWork == "Drawing"
		{
			"\0%(typing):> I'm working on the shell for Alastria<: :> it's pretty chill<: :> though I'm having some trouble getting the eyes to look right<:"
		}
		if CurrentWork == "Writing"
		{
			"\0%(typing):> Dialogues dialogues dialogues<: :> Or the planning for dialogues, anyways<: :> I'm trying to script out how the story is going to go<:"
		}
	}
	if CurrentProject == "Obscure Ghost"
	{
		if CurrentWork == "Programming"
		{
			"\0%(typing):> Working on Super Snail, setting up some menus and stuff<: :> It's not glamorous work but it does need to be done<: :> but it should be a nice clean menu setup<: :> compared to some of my other projects lol<:"
		}
		if CurrentWork == "Drawing"
		{
			"\0%(typing):> Fussing with Super Snail's shell<: :> ghost shell not snail shell<: :> ok a bit of both since I'm touching up the snail shell part of this ghost shell<:"
		}
		if CurrentWork == "Writing"
		{
			"\0%(typing):> working on dialogue for Super Snail<: :> it's kinda sad ngl<: :> I mean that's the hope anyways<:"
		}
	}
	if CurrentProject == "Object Ghost"
	{
		if CurrentWork == "Programming"
		{
			"\0%(typing):> Working on coding stuff for Printer right now<: :> I only have like 2 mechanics in this ghost so they really need to be solid<: ::> and<:: :> right now they're not lol<:"
		}
		if CurrentWork == "Drawing"
		{
			"\0%(typing):> making more drawings for Printer<: :> can never have enough<: :> lots of printouts for lots of collecting and displaying<: :> it's kinda fun<: :> making new printouts is like its own little project<: :> so I feel like I've gotten a lot done after making them<:"
		}
		if CurrentWork == "Writing"
		{
			"\0%(typing):> Printer dialogue<: :> I can basically just dump whatever's in my brain in here lol<: :> I do try to make it printer themed th<: :> like you know<: \i[6] :> BRRRRRRRR-SHK, BRRRRRRRRRR-SHK<: :> printer noises and all<:"
		}
	}
	if CurrentProject == "Idle"
	{
		"\0%(typing):> Working on resting<: \i[6] :> It's exhausting work<:"
		"\0%(typing):> I'm a bit distracted atm tbqh<: :> I'll get back to work later<:"
		"\0%(typing):> Not up to much right now<: :> Just need to rest my brain for a bit<:"
		"\0%(typing):> I'm thinking about what to work on next<: :> but I haven't settled on anything yet lol<:"
		
		//Ghost specific
		"\0%(typing):> Well I'm supposed to be working on Fae Friends Forever<: :> but I'm taking a little break<: :> idk I'll get back to work in a bit<:"
		"\0%(typing):> I'm working on Alastria<: :> in theory<: :> taking a break, you know<:"
		"\0%(typing):> Super Snail is my project for now<: :> not that I'm<: :> doing much at the moment<: :> I got a little distracted<:"
		"\0%(typing):> I'm working on Printer<: :> Or I'm supposed to be<: :> I'm a little tired<: :> So I'm<: :> Having a break<:"
	}
	--
	backbuttons
}

OnTalk.GhostTips : nonoverlap_pool
{
	"\0%(typing):> Read a lot on Ukadoc<: :> It will save your life<: :> Sometimes I just go and browse there<: :> and I learn about lots of things that I didn't know were possible<: :> Seriously just scroll through the sakurascript tags sometime<: :> Did you know there's one for erasing charactesr out of the balloon?<: :> I've found some really surprising uses for that<:"
	"\0%(typing):> Don't be afraid to reach out for help<: :> No point in just feeling lost and confused for hours and tiring yourself out<:"
	"\0%(typing):> Play with stuff<: :> If you wanna know what something does<: :> Just start messing with it<: :> You can always undo your changes later<:"
	"\0%(typing):> Listen to the error log<: :> It's there to help you<: :> sometimes I get mad at it lol<: :> but then a while later I find the error<: :> and then I feel kind of bad because it was just doing its job<: :> It's a lot easier to find errors if you stay calm and read very carefully<: :> learn from my mistakes<:"
	"\0%(typing):> Your first ghost won't be perfect<: :> But that's ok<: :> Keep working at it and you'll get better<:"
	"\0%(typing):> Have an end goal in mind<: :> If your plan is ""I'm gonna make a ghost of X"" then you might reach a point where you just don't know what to do or add<: :> So think about it before you start<: :> What do you \f[italic,1]want\f[italic,default] your ghost to do?<: :> What do you want the key focus to be?<: \w8\w8\w8\s[7]\w8\w8\w8\s[1] :> Make an object ghost first tbh<: :> Something simple and straightforward so you can learn the ropes<:"
	"\0%(typing):> If you're not sure what features to have, or what poses to have, or what sorts of topics your ghost should talk about<: :> go study some other ghosts<: :> Look them over and write down a list of the things you like<: :> Taking a close look and getting that stuff down on paper can help a lot<:"
	"\0%(typing):> Ghost making doesn't have to be a huge task<: :> There are ghost jams where ghosts get made in a week<: :> Sometimes people even submit ghosts within the first day<: :> It's pretty crazy<: :> I'm not that good yet lol<: :> I made Egg for a ghost jam but I took a week on it<: :> I learned a lot tho<: :> It's not a super flashy ghost but it only took 1 week to make<: :> and I was working alone lol<:"
	"\0%(typing):> The best thing to do is just do it<: :> and if it all seems overwhelming<: :> remember that there are no required events in ghosts<: :> you can leave out anything that you don't want to deal with<: :> and that can make the work a lot more manageable and focused<:"
	"\0%(typing):> Make a planning doc<: :> You don't need to get super detailed or anything<: :> just outline what you want<: :> and stick to it<: :> don't let your scope bloat so far that you don't complete your ghost<: :> put new ideas into a section for after-release updates or something<: :> make them goals that you have to complete the main ghost before you go after<: :> that helps me, anyways<:"
	"\0%(typing):> Learn how to use all the features of your program<: :> notepad++ has macros<: :> column editing which is a lifesaver for shell code<: :> you can save sessions for each ghost so you can open their files quickly<: :> all kinds of great key combos<: :> the more I learn the more efficient I get<: :> tho sometimes I learn a feature and it's just never useful for me lol<:"
	--
	backbuttons
}

OnTalk.GhostIdeas : nonoverlap
{
	"\0%(typing):> I saw an interesting one in a group the other day<: :> It was a ghost of %(GhostIdeaGenerator)<: :> kinda bizarre, but<: :> idk seems like it could be fun<:"
	"\0%(typing):> Someone was dumping ideas the other day<: :> A couple were interesting<: :> like a ghost of %(GhostIdeaGenerator)<: :> or a ghost of %(GhostIdeaGenerator)<: :> so there's something for you if you're struggling with what to make<:"
	"\0%(typing):> I saw some people discussing ghost ideas the other day<: :> They were talking about a ghost of %(GhostIdeaGenerator)<: :> They weren't taking it very seriously but like<: :> I bet someone could pull that off<:"
	"\0%(typing):> Sure, I've seen plenty of ideas being thrown out there<: :> Like a ghost of %(GhostIdeaGenerator)<: :> Kind of a weird one but I feel like you could make something of it<:"
	"\0%(typing):> Here's a challenge for you<: :> A ghost of %(GhostIdeaGenerator)<: :> Have fun<:"
	"\0%(typing):> I came up with a random idea myself actually<: :> I was just daydreaming<: :> I don't want it tho, so<: :> If you want it you can have it<: :> The idea was a ghost of %(GhostIdeaGenerator)<:"
	--
	backbuttons + "  \![*]\__q[OnTalk.GhostIdeas]Again\__q"
}

backbuttons
{
	if _argv[0] != "nopause"; "\w8\w8"
	--
	"\w8\w8\n\n\![quicksection,true]\![*]\__q[OnTalkMenu]Back\__q  \![*]\__q[blank]Nevermind\__q"
}

backbuttonsghost
{
	if _argv[0] != "nopause"; "\w8\w8"
	--
	"\n\n\![quicksection,true]\![*]\__q[OnTalk.HisGhosts]Back\__q  \![*]\__q[blank]Nevermind\__q"
}

OnEncourageMenu : all
{
	"\C\![lock,balloonrepaint]\c\0\_q"
	"\![set,autoscroll,disable]"
	
	if !DefaultBalloon; "\b[0]"
	else; "\b[4]"
	
	"\![*]\__q[OnEncourageWork]Get back to work\__q\n"
	"\![*]\__q[OnEncourageBreak]Take a break\__q\n"
	//"\![*]\__q[OnEncourageWater]Stay hydrated\__q\n"
	"\n"
	"\![*]\__q[OnSakuraMenu]Back\__q  \![*]\__q[blank]Nevermind\__q"
	"\![unlock,balloonrepaint]"
}

OnEncourageWork
{
	if CurrentProject != "Idle"
	{
		"\0%(typing):> I'm already working<: :> You can still see my screen right?<:\e"
		"\0%(typing):> I am working<: :> I don't have enough hands to be doing more than this<: :> pls have mercy<:\e"
		"\0%(typing):> I'm already on it<: :> You can watch if you want<: :> I'm screensharing, you know<:\e"
	}
	elseif CurrentMood == "Long"
	{
		SetProjectModeTime("no idle")
		"\0%(typing):> You're right you're right<: :> I need to just get to it<: :> Get in the zone<: :> Focus focus focus<:\e"
		"\0%(typing):> Yes I know<: :> I was just taking a break for a bit<: :> But I need to really push today and get through some of this<:\e"
		"\0%(typing):> Oh, yeah<: :> Got a bit distracted<: :> Back to it, then<:\e"
	}
	elseif CurrentMood == "Short"
	{
		"\0%(typing):> Eh, in a bit<:\e"
		"\0%(typing):> I'll get there, just taking a break<:\e"
		"\0%(typing):> I'll get back to it in a few minutes<:\e"
	}
	else //Random mood
	{
		"\0%(typing):> Ehh<: :> I want a break for a bit longer<: :> Just a bit tired<: :> You know?<:\e"
		"\0%(typing):> I will<: :> But I want to finish up something else first<: :> A few minutes away will let my brain reset too <: :> or something<:\e"
		"\0%(typing):> Yeah, I guess you're right<: :> Let's see<: :> Can I remember where I left off lol<:\![raise,OnEncourageChangeProject,noidle]\e"
		"\0%(typing):> I guess I do have a deadline to meet<: :> Alright, back at it, let's goooo<:\![raise,OnEncourageChangeProject,noidle]\e"
	}
}

OnEncourageBreak
{
	if CurrentMood == "Long"
	{
		"\0%(typing):> I'll take a break in a bit<: :> I'm on a roll<: :> Don't want to lose momentum<:\e"
		"\0%(typing):> Eh, later<: :> I'm in the zone<: :> Gotta make use of this focus while it's here<:\e"
		"\0%(typing):> I'm making a lot of progress though<: :> I'll take a break once I finish this part<:\e"
	}
	elseif CurrentMood == "Short"
	{
		SetProjectModeTime("idle")
		"\0%(typing):> Yeah, I'm not really getting much done anyways<: :> Better to rest my brain for a bit lol<:\e"
		"\0%(typing)\i[6]:> Good idea<: :> Break time<: :> Break time forever<: \s[1] :> break time at least for a bit<:\e"
		"\0%(typing):> I don't want to but I'm struggling to stay on task anyways<: :> So maybe a break is what I need<: :> Just so long as I don't take a break for the rest of the day<: \w8\w8\w8\w8\i[6] :> does sound kind of tempting tho<:\e"
	}
	else //Random mood
	{
		"\0%(typing):> Eh, in a bit<: :> I'm almost done with this<:\e"
		"\0%(typing):> I'll get there<: :> but I really want to get this finished first<: :> I've been trying for weeks and only just now started making real progress<: :> you know?<: :> so I don't want to risk losing my momentum here<:\e"
		"\0%(typing):> Hmm yeah<: :> I want to get caught up on some stuff for a bit<: :> break time break time break time<:\![raise,OnEncourageChangeProject,idle]\e"
		"\0%(typing):> Good idea<: :> Time to drink some water<: :> Get up and get a snack<: :> Stand and do some stretches<: :> Maybe even go for a walk<: \w8\w8\w8\w8\w8\w8 :> or at least think about doing those things<:\![raise,OnEncourageChangeProject,idle]\e"
	}
}

OnEncourageChangeProject
{
	SetProjectModeTime(reference0)
}

// OnEncourageWater
// {
	// if CurrentMood == "Long"
	// {
		// "Hm? Oh, yeah, I forgot, thanks"
	// }
	// elseif CurrentMood == "Short"
	// {
		// "Hydrate or diedrate, nerds"
	// }
	// else //Random mood
	// {
		// "Later"
	// }
// }

OnSettings : all
{
	if !ISVAR("SpecialFont"); SpecialFont = ("",DefaultFontSize)
	"\C\![lock,balloonrepaint]\c\0\_q\f[anchorvisitedfontcolor,default.anchor]"
	"\![set,autoscroll,disable]"
	"\![close,inputbox,OnNewNickname]\![close,inputbox,OnFontSizeSlider]"
	"\f[align,center]%(emdash) Chat Preferences %(emdash)\n\n\f[align,left]"
	
	if !DefaultBalloon; "\b[2]"
	else; "\b[6]"
	
	"Username: \![*]\_a[OnChangeUsername]%(origname)\_a\n"
	"Nickname: \![*]\_a[OnChangeNickname]%(username)\_a"
	if nickname != ""; " \__q[OnResetNickname](reset)\__q"
	"\n\n"
	
	_font = "Default"
	if SpecialFont[0] != ""; _font = SpecialFont[0]
	"Font: \![*]\_a[OnChangeFont]%(_font)\_a (\_a[OnFontSizeSlider]%(SpecialFont[1])\_a) \f[height,default]\q[▲,OnFontSize,up] \q[▼,OnFontSize,down]\f[height,%(SpecialFont[1])]"
	if SpecialFont[0] != "" || SpecialFont[1] != DefaultFontSize; " \__q[OnResetFont](reset)\__q"
	"\n"
	if DefaultBalloon
	{
		"Text color: "
		switch WhiteText
		{
			"\__q[OnToggleTextColor,green]Green\__q  \f[color,disable]\__q[OnToggleTextColor,white]White\__q\f[color,default]"
			"\f[color,disable]\__q[OnToggleTextColor,green]Green\__q\f[color,default]  \__q[OnToggleTextColor,white]White\__q"
		}
	}
	else; "\n"
	"\n\n"
	
	
	"Screenshare: \_a[OnToggleScreenshare]%(('On','Off')[KeroHidden])\_a\n"
	_scales = (25,50,100,150,200,300)
	
	if KeroHidden; "\f[color,disable]"
	
	"Screenshare size:\n"
	
	foreach _scales; _scale
	{
		if KeroHidden; "%(_scale)% "
		else
		{
			_tag = ""
			if KeroScale == _scale; _tag = "\_a"
			else; _tag = "\__q"
			_tag + "[OnChangeKeroScale,%(_scale)]%(_scale)%" + _tag + " "
		}
	}
	"\n\n"
	if KeroHidden; "\f[color,default]"
	
	
	"Message frequency:\n"
	
	_rates = (1,3,5,10,15)
	foreach _rates; _rate
	{
		_time = TOINT(_rate * 60)
		_tag = ""
		if aitalkinterval == _time; _tag = "\_a"
		else; _tag = "\__q"
		_tag + "[OnTalkRateChange,%(_time)]%(_rate)m" + _tag + " "
	}
	
	
	"\n\n"
	"\![*]\__q[OnSakuraMenu]Back\__q  \![*]\__q[blank]Nevermind\__q"
	
	"\![unlock,balloonrepaint]"
}

OnChangeUsername : all
{
	"\C\![lock,balloonrepaint]\c\0\_q"
	"\![set,autoscroll,disable]"
	if !DefaultBalloon; "\b[2]"
	else; "\b[6]"
	
	"\![open,configurationdialog,setup]"
	"\f[italic,1](Username can be changed in the ""User Information"" section at the bottom of the SSP preferences.)\n\n"
	"(After setting your name in the ""Name"" box, close the SSP preferences window and then click below to return to Moth's preferences.)\f[italic,default]\n\n"
	"\![*]\__q[OnSettings]Back to preferences\__q  \![*]\__q[blank]Nevermind\__q"
	"\![unlock,balloonrepaint]"
}

OnChangeNickname : all
{
	"\C\![lock,balloonrepaint]\c\0\b[0]\_q"
	"\![open,inputbox,OnNewNickname,-1]"
	
	"> what should I call you?"
	
	"\n\n\![*]\__q[OnSettings]Back\__q  \![*]\__q[OnCancelInput]Nevermind\__q"
	"\![unlock,balloonrepaint]"
}

OnNewNickname : all
{
	nickname = reference0
	OnSettings
	// "\_q> k, %(nickname) it is.\_q\w8\w8\n\n:> cool.<:\w8\w8"
	
	// "\n\n\_q\![*]\__q[OnSettings]Back\__q  \![*]\__q[blank]Done\__q"
	// "\_w[%((RAND(3) + 1) * 1000)]%(OnSurfaceRestore)"
}

OnCancelInput
{
	"\C\![lock,balloonrepaint]\![close,inputbox,OnNewNickname]\![close,inputbox,OnFontSizeSlider]%(OnSurfaceRestore)"
}

OnResetNickname
{
	nickname = ""
	OnSettings
}

OnNotifyFontInfo
{
	FontList = reference
}

OnChangeFont : all
{
	_totalpages = (ARRAYSIZE(FontList) / 10) - 1
	if ARRAYSIZE(FontList) % 10 != 0; _totalpages++
	
	if reference0 == ""; reference0 = 0
	if reference0 < 0; reference0 = 0
	if reference0 > _totalpages; reference0 = _totalpages
	
	"\C\![lock,balloonrepaint]\c\0\_q"
	"\![set,autoscroll,disable]"
	if !DefaultBalloon; "\b[2]"
	else; "\b[6]"
	
	"\f[align,center]%(emdash) Font Selection %(emdash)\n\f[align,left]"
	
	switch reference0 == 0
	{
		"\q[<<,OnChangeFont,%(reference0 - 1)]"
		"\f[color,disable]<<\f[color,default]"
	}
	"\n \n[-100]\f[align,center] Page %(reference0 + 1) / %(_totalpages + 1) \n \n[-100]\f[align,right]"
	
	switch reference0 >= _totalpages
	{
		"\q[>>,OnChangeFont,%(reference0 + 1)]"
		"\f[color,disable]>>\f[color,default]"
	}
	"\n\n\f[align,left]"
	"\![*]\__q[OnNewFont,]Reset to default\__q\n\n"
	
	
	for _i = reference0 * 10; _i < ARRAYSIZE(FontList) && _i < (reference0 + 1) * 10; _i++
	{
		_font = FontList[_i]
		"\![*]\f[name,""%(_font)""]\__q[OnNewFont,""%(_font)""]%(_font)\__q\n"
	}
	
	if SpecialFont[0] != ""; "\f[name,%(SpecialFont[0])]"
	else; "\f[name,default]"
	"\n\![*]\__q[OnSettings]Back\__q  \![*]\__q[blank]Nevermind\__q"
	"\![unlock,balloonrepaint]"
}

OnNewFont
{
	SpecialFont[0] = reference0
	OnSettings
}

OnFontSize
{
	if reference0 == "up"
	{
		SpecialFont[1]++
	}
	else //down
	{
		SpecialFont[1]--
	}
	if SpecialFont[1] <= 1; SpecialFont[1] = 1
	OnSettings
}

OnFontSizeSlider : all
{
	"\0\_q"
	"\![open,sliderinput,OnFontSizeSlider,-1,14,1,100]"
	
	if !DefaultBalloon; "\b[2]"
	else; "\b[6]"
	
	if reference0 != ""; SpecialFont[1] = TOINT(reference0)
	
	"Current font size: %(SpecialFont[1])"
	
	"\n\n\![*]\__q[OnSettings]Back\__q  \![*]\__q[OnCancelInput]Nevermind\__q"
}

OnResetFont
{
	SpecialFont = ("",DefaultFontSize)
	OnSettings
}

OnToggleTextColor
{
	if reference0 == "white"; WhiteText = 1
	else; WhiteText = 0
	OnSettings
}

OnToggleScreenshare
{
	"\C\![lock,balloonrepaint]\1\![bind,Hide,Toggle]"
}

OnChangeKeroScale
{
	KeroScale = reference0
	"\C\![lock,balloonrepaint]\1\![set,scaling,%(KeroScale)]\0\![raise,OnSettings]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	OnSettings
}