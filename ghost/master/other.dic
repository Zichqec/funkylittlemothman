OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	_font = ""
	if SpecialFont[0] != ""; _font += "\f[name,%(SpecialFont[0])]"
	if SpecialFont[1] != DefaultFontSize; _font += "\f[height,%(SpecialFont[1])]"
	
	if DefaultBalloon && WhiteText
	{
		_font += "\f[color,255,255,255]"
		_font += "\f[cursorpencolor,176,243,083]"
		_font += "\f[cursorfontcolor,176,243,083]"
		_font += "\f[anchorfontcolor,176,243,083]"
		_font += "\f[anchorpencolor,176,243,083]"
	} 
	
	_wait = "1.25"
	
	_typing = ""
	_typing += "\_l[0,]\![set,balloonwait,%(_wait)]"
	_typing += "\f[color,disable]\_q\_l[,]"
	_typing += "\n\n[half]\f[align,left]"
	_typing += "@funkylittlemothman is typing...\_q\![lock,balloonrepaint]"
	_typing += "\c[line,1]\n\n[half]\f[color,default]"
	
	_first = 1
	while ":> " _in_ _talk || "::> " _in_ _talk || "[[" _in_ _talk
	{
		LOGGING("====================================================================")
		_normaltalk = STRSTR(_talk,":> ",0)
		_fasttalk = STRSTR(_talk,"::> ",0)
		_narrationtalk = STRSTR(_talk,"[[",0)
		
		//AHAHAHAHAHA I'M A GENIUS (not really there are 13 hours to go im dying please dont judge this or use it as good code to reference)
		if _normaltalk == -1; _normaltalk = 999999999999999999
		if _fasttalk == -1; _fasttalk = 999999999999999999
		if _narrationtalk == -1; _narrationtalk = 999999999999999999
		
		LOGGING("_first: %(_first)")
		if _normaltalk < _fasttalk && _normaltalk < _narrationtalk && _normaltalk > -1 //:>
		{
			LOGGING("NORMAL TALK")
			_toadd = "%(_typing)\f[align,left]> "
			//if !_first; _toadd = "\n\n[half]" + _toadd
			if !_first; _toadd = "\w8\w8\w8" + _toadd
			
			_talk = REPLACE(_talk,":> ",_toadd,1)
		}
		elseif _fasttalk < _normaltalk && _fasttalk < _narrationtalk && _fasttalk > -1 //::>
		{
			LOGGING("FAST TALK")
			_toadd = "%(_typing)\f[align,left]> "
			//if !_first; _toadd = "\n\n[half]" + _toadd
			if !_first; _toadd = "\w8" + _toadd
			
			_talk = REPLACE(_talk,"::> ",_toadd,1)
		}
		else //[[
		{
			LOGGING("NARRATION TALK")
			LOGGING("_normaltalk: %(_normaltalk)")
			LOGGING("_fasttalk: %(_fasttalk)")
			LOGGING("_narrationtalk: %(_narrationtalk)")
			
			_toadd = "%(_typing)\f[align,center]\f[bold,1]\f[color,default.anchor]@funkylittlemothman\f[color,default]\f[bold,default] "
			//if !_first; _toadd = "\n\n[half]" + _toadd
			if !_first; _toadd = "\w8\w8\w8" + _toadd
			
			_talk = REPLACE(_talk,"[[",_toadd,1)
		}
		_first = 0
	}
	

	//_talk = REPLACE(_talk,"::> ","%(_typing)> ",1)
	//_talk = REPLACE(_talk,"::> ","\w4\n\n[half]%(_typing)\n\n[half]> ")
	_talk = REPLACE(_talk,"<::","\![set,balloonwait,1]\w8\![unlock,balloonrepaint]")

	//_talk = REPLACE(_talk,":> ","%(_typing)\_l[0,]> ",1)
	//_talk = REPLACE(_talk,":> ","\w8\w8\w8\n\n[half]%(_typing)\n\n[half]> ")
	_talk = REPLACE(_talk,"<:","\![set,balloonwait,1]\w8\w8\![unlock,balloonrepaint]")
	
	_talk = REPLACE(_talk,"\n\n[half]","",1)
	
	//_talk = REPLACE(_talk,"[[","%(_typing)\_l[0,]\f[align,center]\f[bold,1]\f[color,default.anchor]@funkylittlemothman\f[color,default]\f[bold,default] ")
	_talk = REPLACE(_talk,"]]","\![set,balloonwait,1]\w8\w8\n \n[-100]\f[align,left]\![unlock,balloonrepaint]")
	
	if SHIORI3FW.EventidOld == "OnAITalkNewEvent" || SHIORI3FW.EventidOld == "OnKeyPress" || SHIORI3FW.EventidOld == "OnSecondChange" || SHIORI3FW.EventidOld == "OnLastTalk" || SHIORI3FW.EventidOld == "OnFirstBoot2" || (reference1 == "" && reference2 == "")
	{
		/*if reference1 == "" && reference2 == "" //input box - screw it man its jam and im tired so im being bad about making debug stuff easier
		{
			_talk = REPLACE(_talk,"\e\e","\_w[%((RAND(3) + 1) * 1000)]\![embed,OnSurfaceRestore]",1)
		}
		else;*/ _talk = REPLACE(_talk,"\e","\_w[%((RAND(3) + 2) * 1000)]\![embed,OnSurfaceRestore]",1)

		//_talk = REPLACE(_talk,origname,"\f[bold,1]\f[color,default.anchor]%(origname)\f[color,default]\f[bold,default]")
	}
	
	if DefaultBalloon && WhiteText
	{
		_talk = REPLACE(_talk,"\f[color,default]","\f[color,255,255,255]")
		_talk = REPLACE(_talk,"\f[color,default.anchor]","\f[color,176,243,083]")
		_talk = REPLACE(_talk,"\f[anchorvisitedfontcolor,default.anchor]","\f[anchorvisitedfontcolor,176,243,083]")
	}
	
	
	//_fontfix = " \_l[0,]"
	//; _fontfix = ""
	if SHIORI3FW.EventidOld != "OnSurfaceRestore"; _talk = REPLACE(_talk,"\0","\0%(_font)\_l[,] \_l[0,]",1)
	
	TOSTR(_talk)
}

ChatlogFormatting
{
	_talk = _argv[0]
	
	_talk = REPLACE(_talk,"\f","||f")
	_talk = REPLACE(_talk,"||f[height,","\f[height,")
	_talk = REPLACE(_talk,"||f[name,","\f[name,")
	_talk = SHIORI3FW.RemoveAllTags(_talk)
	
	_typing = ""
	_typing += "\_l[0,]"
	_typing += "\f[align,left]"
	_typing += "\n\n[half]"
	
	_first = 1
	while ":> " _in_ _talk || "::> " _in_ _talk || "[[" _in_ _talk
	{
		LOGGING("====================================================================")
		_normaltalk = STRSTR(_talk,":> ",0)
		_fasttalk = STRSTR(_talk,"::> ",0)
		_narrationtalk = STRSTR(_talk,"[[",0)
		
		//AHAHAHAHAHA I'M A GENIUS (not really there are 13 hours to go im dying please dont judge this or use it as good code to reference)
		if _normaltalk == -1; _normaltalk = 999999999999999999
		if _fasttalk == -1; _fasttalk = 999999999999999999
		if _narrationtalk == -1; _narrationtalk = 999999999999999999
		
		LOGGING("_first: %(_first)")
		if _normaltalk < _fasttalk && _normaltalk < _narrationtalk && _normaltalk > -1 //:>
		{
			_toadd = "%(_typing)\f[align,left]> "
			//if !_first; _toadd = "\n\n[half]" + _toadd
			
			_talk = REPLACE(_talk,":> ",_toadd,1)
		}
		elseif _fasttalk < _normaltalk && _fasttalk < _narrationtalk && _fasttalk > -1 //::>
		{
			_toadd = "%(_typing)\f[align,left]> "
			//if !_first; _toadd = "\n\n[half]" + _toadd
			
			_talk = REPLACE(_talk,"::> ",_toadd,1)
		}
		else //[[
		{
			_toadd = "%(_typing)\f[align,center]\f[bold,1]\f[color,default.anchor]@funkylittlemothman\f[color,default]\f[bold,default] "
			
			_talk = REPLACE(_talk,"[[",_toadd,1)
		}
		_first = 0
	}
	
	_talk = REPLACE(_talk,"<::","")
	_talk = REPLACE(_talk,"<:","")
	
	//_talk = REPLACE(_talk,"\n\n[half]","",1)

	_talk = REPLACE(_talk,"]]","\n \n[-100]\f[align,left]")
	
	_talk = REPLACE(_talk,"||f","\f")
	
	_talk
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore : pool //I suppose I will have to build the kero surfaces into this, because they'll have to be tied to what he's doing... but later when I have those surface numbers. It may be better to set up a series of envelopes for easy post calling during dialogues?
{
	SetSurfaceModeTime

	if CurrentWork == "Programming"; programming
	if CurrentWork == "Writing"; writing
	if CurrentWork == "Drawing"; drawing
	
	if CurrentWork == "Idle"; idling
	else; ghosting
	
	if RAND(4) == 0; slump
	else; thinking
}

//Happens when the ghost is unminimized. You *can* include dialogue, but you don't *have* to. I have included this event because if you don't, it defaults to \0\s[0]\1\s[10], which you don't want in some cases, such as if your ghost uses different surfaces or has a different number of characters.
OnWindowStateRestore
{
	OnSurfaceRestore
}

On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/funkylittlemothman/main/"
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Link label";	"https://link.example.com/link/to/whatever/here/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Another link label";	"https://link.example.com/link/to/whatever/here/again/"
	"A third link label";	"https://link.example.com/link/to/whatever/a/third/time/you/can/add/as/many/as/you/want/"
}

On_kero.recommendsites
{
	FormatLinks(recommendsites_kero)
}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"These labels can be anything you want";	"https://link.example.com/link/to/whatever/here/once/more/"
}


//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	if SHIORI3FW.UserSex == "male"; "he"
	elseif SHIORI3FW.UserSex == "female"; "she"
	else; "they"
}

them
{
	if SHIORI3FW.UserSex == "male"; "him"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "them"
}

their
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "their"
}

theirs
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "hers"
	else; "theirs"
}

themself
{
	if SHIORI3FW.UserSex == "male"; "himself"
	elseif SHIORI3FW.UserSex == "female"; "herself"
	else; "themself"
}

//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if SHIORI3FW.UserSex == "undef"; _argv[0] //they are
	else; _argv[1] //he is/she is
}

/*

An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%('ve','s') found it."

If using they, the %(p) functions will return the first word specified, resulting in:
"they were looking for that earlier, I'll ask if they've found it."

If not using they, for example if using she, it will return the second specified word instead, resulting in:
"she was looking for that earlier, I'll ask if she's found it."

With this function, you can quickly customize words that need to change based on whether it should be singular or plural. Just remember; the %(p) function is set up so that the first word you specify matches with the 'they' labels used on the pronoun functions. You can always switch that around if you like, too.

*/

OnNotifyDressupInfo
{
	KeroHidden = 0
	foreach reference; _ref
	{
		_ref = SPLIT(_ref,C_BYTE1)
		
		LOGGING("_ref: %(_ref)")
		if _ref[0] == "1" && _ref[1] == "Hide" && _ref[2] == "Toggle" && _ref[4] == "1"; KeroHidden = 1
	}
	LOGGING("KeroHidden: %(KeroHidden)")
}

OnDressupChanged
{
	"\C\![raise,OnSettings]"
}