OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
		_talk = REPLACE(_talk,"\e","\_w[%((RAND(3) + 1) * 1000)]\![embed,OnSurfaceRestore]",1) //Debugging purposes - just a dupe of the thing below
	}
	
	_font = ""
	if SpecialFont[0] != ""; _font += "\f[name,%(SpecialFont[0])]"
	if SpecialFont[1] != 14; _font += "\f[height,%(SpecialFont[1])]"
	
	_talk = REPLACE(_talk,":> ","\f[color,disable]\_q@funkylittlemothman is typing...\_q\f[color,default]\![lock,balloonrepaint]\c[char,32]%(_font)> ",1)
	_talk = REPLACE(_talk,":> ","\w8\w5\n\n[half]\f[color,disable]\_q@funkylittlemothman is typing...\_q\f[color,default]\![lock,balloonrepaint]\c[char,32]\n\n[half]%(_font)> ")
	_talk = REPLACE(_talk,"<:","\w8\w5\![unlock,balloonrepaint]")
	
	_talk = REPLACE(_talk,"[[","\![lock,balloonrepaint]\f[align,center]\f[bold,1]@funkylittlemothman\f[bold,default] ")
	_talk = REPLACE(_talk,"]]","\n\f[align,left]\n[-100]\w8\w5\![unlock,balloonrepaint]")
	
	LOGGING("====================================================================")
	LOGGING("SHIORI3FW.EventidOld: %(SHIORI3FW.EventidOld)")
	if SHIORI3FW.EventidOld == "OnAITalkNewEvent" || SHIORI3FW.EventidOld == "OnKeyPress"
	{
		_talk = REPLACE(_talk,"\e","\_w[%((RAND(3) + 1) * 1000)]\![embed,OnSurfaceRestore]",1)
	}
	
	_talk = REPLACE(_talk,"\0","\0%(_font)")
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore : pool //I suppose I will have to build the kero surfaces into this, because they'll have to be tied to what he's doing... but later when I have those surface numbers. It may be better to set up a series of envelopes for easy post calling during dialogues?
{
	SetSurfaceModeTime
	
	"\1"
	--
	if CurrentProject == "" //placeholder
	{
		
	}
	else
	{
		"\s[11]"
	}
	--
	"\0"
	--
	if CurrentWork == "Programming" || CurrentWork == "Writing"
	{
		"\s[2]"
	}
	if CurrentWork == "Programming"
	{
		"\s[3]"
	}
	if CurrentWork == "Drawing"
	{
		"\s[4]"
		"\s[5]"
	}
	"\s[7]" //TODO Unsure if these should be reserved for idling
	"\s[8]"
	"\s[9]"
}

//Happens when the ghost is unminimized. You *can* include dialogue, but you don't *have* to. I have included this event because if you don't, it defaults to \0\s[0]\1\s[10], which you don't want in some cases, such as if your ghost uses different surfaces or has a different number of characters.
OnWindowStateRestore
{
	OnSurfaceRestore
}

On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/funkylittlemothman/main/"
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Link label";	"https://link.example.com/link/to/whatever/here/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Another link label";	"https://link.example.com/link/to/whatever/here/again/"
	"A third link label";	"https://link.example.com/link/to/whatever/a/third/time/you/can/add/as/many/as/you/want/"
}

On_kero.recommendsites
{
	FormatLinks(recommendsites_kero)
}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"These labels can be anything you want";	"https://link.example.com/link/to/whatever/here/once/more/"
}


//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	if SHIORI3FW.UserSex == "male"; "he"
	elseif SHIORI3FW.UserSex == "female"; "she"
	else; "they"
}

them
{
	if SHIORI3FW.UserSex == "male"; "him"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "them"
}

their
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "their"
}

theirs
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "hers"
	else; "theirs"
}

themself
{
	if SHIORI3FW.UserSex == "male"; "himself"
	elseif SHIORI3FW.UserSex == "female"; "herself"
	else; "themself"
}

//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if SHIORI3FW.UserSex == "undef"; _argv[0] //they are
	else; _argv[1] //he is/she is
}

/*

An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%('ve','s') found it."

If using they, the %(p) functions will return the first word specified, resulting in:
"they were looking for that earlier, I'll ask if they've found it."

If not using they, for example if using she, it will return the second specified word instead, resulting in:
"she was looking for that earlier, I'll ask if she's found it."

With this function, you can quickly customize words that need to change based on whether it should be singular or plural. Just remember; the %(p) function is set up so that the first word you specify matches with the 'they' labels used on the pronoun functions. You can always switch that around if you like, too.

*/

username
{
	if nickname == ""; origname
	else; nickname
}

origname
{
	"@" + REPLACE(TOLOWER(SHIORI3FW.UserNameFull)," ","_")
}