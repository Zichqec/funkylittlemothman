OnFirstBoot
{
	ProjectModeTime = 0
	WorkModeTime = 0
	MoodModeTime = 0
	CheckModeTimes
	
	stroke = 0
	LastTalk = IARRAY
	nickname = ""
	SpecialFont = ("",DefaultFontSize)
	KeroHidden = 0
	KeroScale = 100
	WhiteText = 0
	
	ProjectsDiscussed = (0,0,0,0,0,0,0,0)
	
	"\t\*\1\s[25]\![move,--base=0,--x=-260,--y=100]\0\s[1]:> Hello<: :> You're a Ghost enthusiast too, right?<: :> Cool. I go by \f[bold,1]\f[color,default.anchor]@funkylittlemothman\f[color,default]\f[bold,default]<: :> But you can just call me Moth<: :> Do I call you %(username), or you got another nickname?<: "
	--
	"\![open,inputbox,OnFirstBoot2,-1]"
	--
	"\n\n\_q\![*]\__q[OnFirstBoot2,,username]%(username) is fine\__q"
}

OnFirstBoot2
{
	if reference0 != ""; nickname = reference0
	"\1\s[25]\0\s[1]"
	--
	if reference1 == "username" || reference0 == ""; "\![close,inputbox,OnFirstBoot2]:> Ok, just %(username)<:"
	else; ":> %(username)?<:"
	--
	" :> Okay<: :> So<: :> You like ghosts<: :> I like ghosts too<: :> I'm mostly into making them<: :> I already made, like, three<: :> You might've seen them before, idk<: :> But yeah lemme share my screen<:\w2\s[3]\w8\w8\1\s[10]\0\w8 :> There<: :> I'll be working on ghosts, and if you want to chat you can message me here\e"
}

OnUserInputCancel
{
	if reference0 == "OnFirstBoot2"; "\C\![open,inputbox,OnFirstBoot2,-1]"
	//elseif reference0 == "OnNewNickname"; //maybe later idk
}

OnInitialize
{
	if !ISVAR("SpecialFont"); SpecialFont = ("",DefaultFontSize)
	if !ISVAR("ProjectsDiscussed"); ProjectsDiscussed = (0,0,0,0,0,0,0,0)
	if reference0 != "reload" //For things you want to reset EXCEPT when just reloading; a convenience for you the dev, basically
	{
		LastTalk = IARRAY
	}
	CheckModeTimes
}

OnBoot
{
	_temp = SHIORI3FW.UserBirthday
	_user_birthday = "%(_temp[1])/%(_temp[2])" //M/D, if you want it D/M you can switch it around
	_today = "%(month)/%(day)" //Again, switch this around if using D/M
	
	"\1\s[10]\![set,scaling,%(KeroScale)]\0\s[0]"
	--
	if _today == _user_birthday
	{
		"\0%(typing):> Birthdays are one day<: :> Ghosts are forever<: :> but yes happy birthday<: :> Ghost time?\e"
	}
	elseif _today == "1/1" //You can add whatever cool dates you want with more elseifs here
	{
		"\0\s[0]:> New year, new ghosts<: :> Just kidding I have too many projects already<:\e"
	}
	else //Normal boot
	{
		"\0%(typing):> OKAY break is over<: :> Let's work on some ghosts<:\e"
		"\0%(typing):> Just woke up<: :> Don't question my sleep schedule<: :> Time to code<:\e"
		"\0%(typing):> Heyo<: :> You're up?<: :> I'm up<: :> For some ghost work<:\e"
		"\0%(typing):> Gaming break is over<: :> Time to work on some ghosts while my energy is restoring<:\e"
		"\0%(typing)\i[6]:> Finally home!!!<: :> Full of ideas!!!<: :> work time work time<:\e"
		"\0%(typing):> Hey<: :> It's this time of day<: :> LET'S GO<:\e"
		"\0%(typing)\i[6]:> I GOT IT<: :> I FIGURED IT OUT<: :> In theory<: :> Let's see the actual code and see<:\e"
		"\0%(typing):> %(username) you here?<: :> %(username)<: :> %(username)<: :> Ghosts are calling<:\e"
		"\0%(typing):> Hey hey hey<: :> I'm all fired up to work<: :> Let's goooo<:\e"
		"\0%(typing):> Just had some food<: :> Ready to work now<:\e"
	}
}

OnClose
{
	//Sometimes his screenshare goes off before he leaves, sometimes not. idk thought it would be nice
	"\0%(typing):> You're done? <: :> I'll work some more and then call it a day<: :> Catch you later<:"
	"\1\s[25]\0\s[1]:> Tireeeed<: :> Gonna pick this up later<: :> Cyaaaa<:"
	"\1\s[25]\0\s[1]:> Okay I made some progress<: :> Enough for now<: :> Cya<:"
	"\1\s[25]\0\s[1]:>\i[6] I'm done I'm done I'm done<: :> It is finished it is done<: :> I will leave bug testing for later:> But for now I'M FREE<:"
	"\1\s[25]\0\s[1]:> I think I'll take a break now<: :> My focus is gone and I can't recatch it<: :> Hopefully I can focus more later<:"
	"\0%(typing):> It's snack tiiiiime<: :> I've been hungry for like the past hour but I really wanted to finish this<: :> Cyaaaa<:"
	"\0%(typing):> You're done?<: :> Cool<: :> Catch you later @lligator<:"
	"\0%(typing):> I did like<: :> nothing today<: :> I hope you acomplished more<:"
	"\1\s[25]\0\s[1]:> Okay I gotta run<: :> Hanging out with a friend<: :> Cya later for some more ghost shenanigans<:"
	"\1\s[25]\0\s[1]:> That's enough for today<: :> Break time<: :> You take a break too, okay?<:"
	"\1\s[25]\0\s[1]:> I finished my thing<: :> Probably<: :> For now<: :> Hopefully<: :> Anyways, later<:"
	"\0%(typing):> You know what<: :> I wanna play some games<: :> Ghost work can wait<:"
	--
	"\w8\w8\-" //This is what makes the ghost close (after a short pause), do not remove the \- , and MAKE SURE you don't use a \e before this point or it won't work
}